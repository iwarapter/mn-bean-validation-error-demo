plugins {
    id 'java-library'
    id 'org.hidetake.swagger.generator' version '2.18.1'
}

repositories {
    jcenter()
}

dependencies {
//    swaggerCodegen 'io.swagger:swagger-codegen-cli:2.4.2'             // Swagger Codegen V2
    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.14'  // or Swagger Codegen V3
//    swaggerCodegen 'org.openapitools:openapi-generator-cli:3.3.4'     // or OpenAPI Generator

    annotationProcessor platform("io.micronaut:micronaut-bom:$micronautVersion")
    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut:micronaut-validation"
    // annotationProcessor "io.micronaut.configuration:micronaut-openapi"

    implementation platform("io.micronaut:micronaut-bom:$micronautVersion")
    implementation "io.micronaut:micronaut-inject"
    implementation "io.micronaut:micronaut-validation"
    implementation "io.micronaut:micronaut-runtime"
    implementation "io.swagger.core.v3:swagger-annotations"
}

swaggerSources {
    petstore {
        inputFile = file('petstore.yaml')
        code {
            language = 'micronaut'
            components = ['models']
        }
    }
}

compileJava.dependsOn swaggerSources.petstore.code
sourceSets.main.java.srcDir "${swaggerSources.petstore.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.petstore.code.outputDir}/src/main/resources"